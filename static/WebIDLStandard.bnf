// Web IDL spec modified from https://www.w3.org/TR/2012/CR-WebIDL-20120419/#idl-grammar.
// Modifications:
// [51]: Changed to refer to [74-77] as per: https://www.w3.org/TR/2012/CR-WebIDL-20120419/#idl-extended-attributes.
[1]	Definitions	→	ExtendedAttributeList Definition Definitions
 | ε
[2]	Definition	→	CallbackOrInterface
 | Partial
 | Dictionary
 | Exception
 | Enum
 | Typedef
 | ImplementsStatement
[3]	CallbackOrInterface	→	"callback" CallbackRestOrInterface
 | Interface
[4]	CallbackRestOrInterface	→	CallbackRest
 | Interface
[5]	Interface	→	"interface" identifier Inheritance "{" InterfaceMembers "}" ";"
[6]	Partial	→	"partial" PartialDefinition
[7]	PartialDefinition	→	PartialInterface
 | PartialDictionary
[8]	PartialInterface	→	"interface" identifier "{" InterfaceMembers "}" ";"
[9]	InterfaceMembers	→	ExtendedAttributeList InterfaceMember InterfaceMembers
 | ε
[10]	InterfaceMember	→	Const
 | AttributeOrOperation
[11]	Dictionary	→	"dictionary" identifier Inheritance "{" DictionaryMembers "}" ";"
[12]	DictionaryMembers	→	ExtendedAttributeList DictionaryMember DictionaryMembers
 | ε
[13]	DictionaryMember	→	Type identifier Default ";"
[14]	PartialDictionary	→	"dictionary" identifier "{" DictionaryMembers "}" ";"
[15]	Default	→	"=" DefaultValue
 | ε
[16]	DefaultValue	→	ConstValue
 | string
[17]	Exception	→	"exception" identifier Inheritance "{" ExceptionMembers "}" ";"
[18]	ExceptionMembers	→	ExtendedAttributeList ExceptionMember ExceptionMembers
 | ε
[19]	Inheritance	→	":" identifier
 | ε
[20]	Enum	→	"enum" identifier "{" EnumValueList "}" ";"
[21]	EnumValueList	→	string EnumValues
[22]	EnumValues	→	"," string EnumValues
 | ε
[23]	CallbackRest	→	identifier "=" ReturnType "(" ArgumentList ")" ";"
[24]	Typedef	→	"typedef" ExtendedAttributeList Type identifier ";"
[25]	ImplementsStatement	→	identifier "implements" identifier ";"
[26]	Const	→	"const" ConstType identifier "=" ConstValue ";"
[27]	ConstValue	→	BooleanLiteral
 | FloatLiteral
 | integer
 | "null"
[28]	BooleanLiteral	→	"true"
 | "false"
[29]	FloatLiteral	→	float
 | "-" "Infinity"
 | "Infinity"
 | "NaN"
[30]	AttributeOrOperation	→	"stringifier" StringifierAttributeOrOperation
 | Attribute
 | Operation
[31]	StringifierAttributeOrOperation	→	Attribute
 | OperationRest
 | ";"
[32]	Attribute	→	Inherit ReadOnly "attribute" Type identifier ";"
[33]	Inherit	→	"inherit"
 | ε
[34]	ReadOnly	→	"readonly"
 | ε
[35]	Operation	→	Qualifiers OperationRest
[36]	Qualifiers	→	"static"
 | Specials
[37]	Specials	→	Special Specials
 | ε
[38]	Special	→	"getter"
 | "setter"
 | "creator"
 | "deleter"
 | "legacycaller"
[39]	OperationRest	→	ReturnType OptionalIdentifier "(" ArgumentList ")" ";"
[40]	OptionalIdentifier	→	identifier
 | ε
[41]	ArgumentList	→	Argument Arguments
 | ε
[42]	Arguments	→	"," Argument Arguments
 | ε
[43]	Argument	→	ExtendedAttributeList OptionalOrRequiredArgument
[44]	OptionalOrRequiredArgument	→	"optional" Type ArgumentName Default
 | Type Ellipsis ArgumentName
[45]	ArgumentName	→	ArgumentNameKeyword
 | identifier
[46]	Ellipsis	→	"..."
 | ε
[47]	ExceptionMember	→	Const
 | ExceptionField
[48]	ExceptionField	→	Type identifier ";"
[49]	ExtendedAttributeList	→	"[" ExtendedAttribute ExtendedAttributes "]"
 | ε
[50]	ExtendedAttributes	→	"," ExtendedAttribute ExtendedAttributes
 | ε
[51]	ExtendedAttribute	→	ExtendedAttributeNamedArgList
 | ExtendedAttributeIdent
 | ExtendedAttributeArgList
 | ExtendedAttributeNoArgs
[52]	ExtendedAttributeRest	→	ExtendedAttribute
 | ε
[53]	ExtendedAttributeInner	→	"(" ExtendedAttributeInner ")" ExtendedAttributeInner
 | "[" ExtendedAttributeInner "]" ExtendedAttributeInner
 | "{" ExtendedAttributeInner "}" ExtendedAttributeInner
 | OtherOrComma ExtendedAttributeInner
 | ε
[54]	Other	→	integer
 | float
 | identifier
 | string
 | other
 | "-"
 | "."
 | "..."
 | ":"
 | ";"
 | "<"
 | "="
 | ">"
 | "?"
 | "Date"
 | "DOMString"
 | "Infinity"
 | "NaN"
 | "any"
 | "boolean"
 | "byte"
 | "double"
 | "false"
 | "float"
 | "long"
 | "null"
 | "object"
 | "octet"
 | "or"
 | "optional"
 | "sequence"
 | "short"
 | "true"
 | "unsigned"
 | "void"
 | ArgumentNameKeyword
[55]	ArgumentNameKeyword	→	"attribute"
 | "callback"
 | "const"
 | "creator"
 | "deleter"
 | "dictionary"
 | "enum"
 | "exception"
 | "getter"
 | "implements"
 | "inherit"
 | "interface"
 | "legacycaller"
 | "partial"
 | "setter"
 | "static"
 | "stringifier"
 | "typedef"
 | "unrestricted"
[56]	OtherOrComma	→	Other
 | ","
[57]	Type	→	SingleType
 | UnionType TypeSuffix
[58]	SingleType	→	NonAnyType
 | "any" TypeSuffixStartingWithArray
[59]	UnionType	→	"(" UnionMemberType "or" UnionMemberType UnionMemberTypes ")"
[60]	UnionMemberType	→	NonAnyType
 | UnionType TypeSuffix
 | "any" "[" "]" TypeSuffix
[61]	UnionMemberTypes	→	"or" UnionMemberType UnionMemberTypes
 | ε
[62]	NonAnyType	→	PrimitiveType TypeSuffix
 | "DOMString" TypeSuffix
 | identifier TypeSuffix
 | "sequence" "<" Type ">" Null
 | "object" TypeSuffix
 | "Date" TypeSuffix
[63]	ConstType	→	PrimitiveType Null
 | identifier Null
[64]	PrimitiveType	→	UnsignedIntegerType
 | UnrestrictedFloatType
 | "boolean"
 | "byte"
 | "octet"
[65]	UnrestrictedFloatType	→	"unrestricted" FloatType
 | FloatType
[66]	FloatType	→	"float"
 | "double"
[67]	UnsignedIntegerType	→	"unsigned" IntegerType
 | IntegerType
[68]	IntegerType	→	"short"
 | "long" OptionalLong
[69]	OptionalLong	→	"long"
 | ε
[70]	TypeSuffix	→	"[" "]" TypeSuffix
 | "?" TypeSuffixStartingWithArray
 | ε
[71]	TypeSuffixStartingWithArray	→	"[" "]" TypeSuffix
 | ε
[72]	Null	→	"?"
 | ε
[73]	ReturnType	→	Type
 | "void"
[74]	ExtendedAttributeNoArgs	→	identifier
[75]	ExtendedAttributeArgList	→	identifier "(" ArgumentList ")"
[76]	ExtendedAttributeIdent	→	identifier "=" identifier
[77]	ExtendedAttributeNamedArgList	→	identifier "=" identifier "(" ArgumentList ")"
